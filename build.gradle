/*
 * chunkstories-api
 * Collection of interfaces forming an API and the skeleton of the chunkstories project.
 */

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven'

//TODO Make version depend on Git ?
version = '115'

group = 'io.xol.chunkstories'
description = 'Open API for the game \'Chunk Stories\''

repositories {
	mavenLocal()
	mavenCentral()
}

ext {
	useEclipseCompiler = true
}

buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		classpath 'com.github.johni0702:gradle-ecj-plugin:master-SNAPSHOT'
	}
}

if(ext.useEclipseCompiler) {
	apply plugin: 'de.johni0702.ecj'
	
	println "Using ECJ compiler"
}

compileJava {
	options.encoding = 'UTF-8'

	//Do we need this ?
	//options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jar {
	//I don't like numbers in my jars
	version = null
}

compileJava.options.debugOptions.debugLevel = "source,lines,vars"

configurations {
	deployerJars
}

dependencies {
	compile group: 'org.joml', name: 'joml', version: '1.9.3'
	compile group: 'io.xol.enklume', name: 'enklume', version: 100
	
	deployerJars "org.apache.maven.wagon:wagon-ssh:2.12" //<-- fuck this fuck this fuck this
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

//Bundle that with the API jar :)
artifacts {
	archives sourcesJar
	archives javadocJar
}

javadoc {
	//Our current javadoc isn't exactly too compliant :')
	failOnError = false
}

//Create a properties.gradle with login credentials to use this.
//Watch out: wagon-ssh is an old mess and WILL NOT TAKE EdDSA host keys
//You MUST ask the server for one of those and replace whatever you had for it in known_hosts
if(hasProperty('uploadUsername')) {
	println 'Login credentials found'
	uploadArchives {
		repositories {
				mavenDeployer {
       				configuration = configurations.deployerJars	
					
					if(project.hasProperty('uploadPrivateKey')) {
						println 'Found private key'
						repository(url: "scp://xol.io/home/maven-user/maven-repo") {				
							authentication(userName: uploadUsername, privateKey: uploadPrivateKey)
		    				}
					}
					else {
						println 'Found password'
						repository(url: "scp://xol.io/home/maven-user/maven-repo") {				
							authentication(userName: uploadUsername, password: uploadPassword)
		    				}
					}
				}
		}
	}
}
